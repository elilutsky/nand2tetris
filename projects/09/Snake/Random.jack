class Random {
    field int seed;
    
    constructor Random new(int newSeed) {
        let seed = newSeed;
        return this;
    }
	
    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
	
	/** return a random number in the range 0..32767 */
    method int rand() {
        let seed = seed + 20251;
        if (seed < 0) {
            let seed = seed - 32767 - 1;
        }
        return seed;
    }
	
	/** return a random number in the range 0..range */
    method int randRange(int range) {
        var int mask;
        var int ret;
        let mask = 1;
        while (mask < range) {
            let mask = mask * 2 + 1;
        }
        let ret = rand() & mask;
        while (ret > range) {
            let ret = rand() & mask;
        }
        return ret;
    }
}